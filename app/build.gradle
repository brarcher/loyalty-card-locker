apply plugin: 'com.android.application'
apply plugin: 'findbugs'

findbugs {
    sourceSets = []
    ignoreFailures = false
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "protect.card_locker"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 5
        versionName "0.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable "GoogleAppIndexingWarning"
        disable "ButtonStyle"
        disable "AlwaysShowAction"
    }

    // This is for Robolectric support for SDK 23
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.journeyapps:zxing-android-embedded:3.2.0@aar'
    compile 'com.google.zxing:core:3.2.1'
    compile 'org.apache.commons:commons-csv:1.2'
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.0"
}

task findbugs(type: FindBugs, dependsOn: assembleDebug) {

    description 'Run findbugs'
    group 'verification'

    classes = fileTree('build/intermediates/classes/debug/')
    source = fileTree('src/main/java')
    classpath = files()

    effort = 'max'

    excludeFilter = file("./config/findbugs/exclude.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
